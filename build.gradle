import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.cfnstacks.gradle.artifacts3' version '0.1.8'
}

group = 'com.cfnstacks'
description = 'Components'

ext.params = [:]
if(project.file('local-config.groovy').exists()) { apply from: 'local-config.groovy' }
def setParam(name, defaultValue) { (hasProperty(name)) ? property(name) : (params[name]) ? params[name] :defaultValue }

ext.params.with {
    failure = setParam('failure', 'ROLLBACK') // DO_NOTHING | ROLLBACK | DELETE
    group = setParam('group', 'com.cfnstacks')
    profile = setParam('profile', '')
    region = setParam('region', 'us-east-2')
    repo = setParam('repo', 'cfn-stacks.com.s3.us-east-2.amazonaws.com')
    stackParamSet = setParam('stackParamSet', 'template')
    template = setParam('template', 'multi-vpc.yaml')
}

ext.stackParameters = [
    template: [:] // template only, not launching any stacks
]

artifacts3 {
    group = params.group
    profileName = params.profile
    repo = params.repo
}

aws {
    profileName = params.profile
    region = params.region
}

cloudFormation {
    templateFile = project.file("build/cloudformation/${params.template}")
    stackName = params.stack
    capabilityIam true
    onFailure params.failure
    conventionMapping.stackParams = { project.ext.stackParameters[project.ext.params.stackParamSet] }
}

asciidoctor {
    backends 'html5', 'pdf'
    attributes \
        'build-gradle': file('build.gradle'),
            'stylesheet': 'stylesheets/style.css',
            'source-highlighter': 'coderay',
            'imagesdir': 'images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': '',
            'projectVersion': project.version
}

task foo(type: Sync) {
    from 'src/main/cloudformation'
    into 'build/cloudformation'

    // When templates need to refer to other templates within the same artifact they
    // can use the @templatePath@ token which will compute the path
    String path = "${(project.version.contains('SNAPSHOT')) ? 'snapshot' : 'release'}/${project.group.replace('.', '/')}/${project.version}"
    println(path)
    filter(ReplaceTokens, tokens: [
            artifactId: project.name,
            version: project.version,
            templatePath: path
    ])
}